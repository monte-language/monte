EOL = tok('EOL') ^(end of line)
EOLs = EOL+
br = EOL*

string = tok('.String.')
character = tok('.char.'):c -> t.Character(c)
float = tok('.float64.')
integer = tok('.int.')

identifier = tok('IDENTIFIER')
uriGetter = tok('URI_GETTER'):s -> t.URIGetter(s)
uri = tok('URI'):u -> t.URIExpr(*u.split(':', 1))

literal = (string | character | float | integer):x -> t.LiteralExpr(x)

noun = sourceHole | justNoun
justNoun = ((identifier:id -> self.keywordCheck(id)
              | ("::" (string | identifier):x -> x)):n  -> t.NounExpr(n)
              | uriGetter)
sourceHole = ('${' integer:i '}' -> t.QuasiLiteralExpr(i)
             |'@{' integer:i '}' -> t.QuasiPatternExpr(i))

quasiString = subquasi*:qs 'QUASI_CLOSE':qc -> qs + [t.QuasiText(qc)]
subquasi = ('QUASI_OPEN':q -> t.QuasiText(q)
           |'${' seq:e '}' -> t.QuasiExprHole(e)
           |'_' !(noIgnoreExpressionHole())
           |'DOLLAR_IDENT':id -> t.QuasiExprHole(t.NounExpr(id))
           |'@{' br pattern:s '}' -> t.QuasiPatternHole(s)
           |"AT_IDENT":id -> t.QuasiPatternHole(t.FinalPattern(t.NounExpr(id), None)))

reifyExpr = ("&&" noun:v -> t.BindingExpr(v)
            |"&" noun:v -> t.SlotExpr(v))

verb = identifier | string

comprehension = 'for' forPattern:p 'in' br assign:a -> p + [a]
listAndMap = "[" ( expr:e br comprehension:c ('if' expr)?:f  ']' -> t.ListComp(*(c + [f, e]))
                 | expr:k "=>" expr:v br comprehension:c ('if' expr)?:f ']' -> t.MapComp(*(c + [f, k, v]))
                 | assoc:x ("," assoc)*:xs ","? ']' -> t.MapExpr([x] + xs)
                 | (expr:s ("," expr)*:ss ","? ']')-> t.ListExpr([s] + ss)
                 | ']' -> t.ListExpr([]))
assoc = (expr:k "=>" expr:v -> t.MapExprAssoc(k, v)
          |"=>" (noun | reifyExpr):n -> t.MapExprExport(n)
                | "def" noun:n ~":="
                  !(throwSemanticHere("Reserved syntax: forward export")))


prim = ( literal
       | basic
       | identifier?:n quasiString:qs -> t.QuasiExpr(n, qs)
       | noun
       | uri
       | parenExpr:p (quasiString:qs -> t.QuasiExpr(p, qs)
                       | -> p)
       | block:b -> t.HideExpr(b)
       | listAndMap
       )

parenExpr = "(" expr:s ')' -> s
block = "{" (seq |-> t.SeqExpr([])):s '}' -> s
indentBlock = ':' EOLs tok('INDENT') br pass (indentSeq |-> t.SeqExpr([])):s br
               tok('DEDENT') -> s
pass = tok('pass')?

seqSep = (";"| EOL)+
seq = blockExpr:e ((seqSep blockExpr)+:es seqSep? -> t.SeqExpr(filter(None, [e] + es))
                  |seqSep? -> e)
indentSeq = blockExpr:e ((EOLs blockExpr)+:es seqSep? -> t.SeqExpr(filter(None, [e] + es))
                        | br -> e)
blockExpr = indentDocoDef | indentEscapeExpr | indentForExpr | indentIfExpr | indentSwitchExpr | indentTryExpr | indentWhileExpr | indentWhenExpr | expr
parenArgs = "(" args:a ')' -> a
args = (expr:s ("," expr)*:ss -> [s] + ss
         |-> [])

call = (prim:p ("." verb:v (parenArgs:x -> (t.MethodCallExpr, v, x)
                                  | -> (t.VerbCurryExpr, v))
               |"[" args:a ']' -> (t.GetExpr, a)
               |parenArgs:x -> (t.FunctionCallExpr, x)
               | "<-" (parenArgs:x -> (t.FunctionSendExpr, x)
               |verb:v (parenArgs:x -> (t.MethodSendExpr, v, x)
               | -> (t.SendCurryExpr, v)))
               )*:ts -> self.collapseTrailers(p, ts)
         )
guarded = call:c (':' guard:g -> t.Coerce(c, g)
                 | -> c)
prefix = (guarded
           | "-" prim:c -> t.Minus(c)
           | "!" call:c -> t.LogicalNot(c)
           | "~" call:c -> t.BinaryNot(c)
           | reifyExpr
           | "&" call !(throwSemanticHere("reserved: unary prefix '&' applied to non-noun lValue")))


assign = ("def" (pattern:p ("exit" order)?:e ":=" assign:a -> t.Def(p, e, a)
                  |noun:n (~~seqSep | end)-> t.Forward(n))
           |keywordPattern:p ":=" assign:a -> t.Def(p, None, a)
           |infix:x (":=" assign:y -> t.Assign(x, y)
                    |assignOp:o assign:y -> t.AugAssign(o, x, y)
                    |tok('VERB_ASSIGN'):v (parenArgs:y -> t.VerbAssign(v, x, y)
                                          |assign:y -> t.VerbAssign(v, x, [y]))
                    | -> x))

assignOp = ("+=" -> "Add"
             |"-=" -> "Subtract"
             |"*=" -> "Multiply"
             |"/=" -> "Divide"
             |"%=" -> "Remainder"
             |"%%=" -> "Mod"
             |"**=" -> "Pow"
             |"//=" -> "FloorDivide"
             |">>=" -> "ShiftRight"
             |"<<=" -> "ShiftLeft"
             |"&=" -> "BinaryAnd"
             |"|=" -> "BinaryOr"
             |"^=" -> "BinaryXor")

expr =  assign | ejector
ejector = ('break' -> t.Break
             | 'continue' -> t.Continue
             | 'return' -> t.Return):ej
             (("(" ')' -> None) | assign
              | indentObjectExpr:o -> t.Object("", *o))?:val -> ej(val)

guard = (noun | parenExpr):e ("[" args:x ']' -> x)*:xs -> t.Guard(e, xs)
optGuard = (':' guard)?
eqPattern = ('_' optGuard:e -> t.IgnorePattern(e)
              |identifier?:n quasiString:q -> t.QuasiPattern(n, q)
              |namePattern
              |"==" prim:p -> t.SamePattern(p)
              |"!=" prim:p -> t.NotSamePattern(p)
              )

patterns = (pattern:p ("," pattern)*:ps -> [p] + ps
             | -> [])
key = (parenExpr | literal):x br -> x

keywordPattern = ('var' noun:n optGuard:g -> t.VarPattern(n, g)
                   |'bind' noun:n optGuard:g -> t.BindPattern(n, g))
namePattern = (keywordPattern
              |noun:n optGuard:g -> t.FinalPattern(n, g)
              |reifyPattern)

reifyPattern = ("&&" noun:n -> t.BindingPattern(n)
               |"&" noun:n optGuard:g -> t.SlotPattern(n, g))

mapPatternAddressing = (key:k "=>" pattern:v -> t.MapPatternAssoc(k, v)
                         |"=>" namePattern:p -> t.MapPatternImport(p))

mapPattern = mapPatternAddressing:a (":=" order:d -> t.MapPatternOptional(a, d)
                                        | -> t.MapPatternRequired(a))
mapPatts = mapPattern:m ("," mapPattern)*:ms -> [m] + ms

listPatternInner = (mapPatts:ms br ']' ("|" listPattern)?:tail -> t.MapPattern(ms, tail)
                     |patterns:ps br ']' ("+" listPattern)?:tail -> t.ListPattern(ps, tail))
listPattern = (
                "via" parenExpr:e listPattern:p -> t.ViaPattern(e, p)
                | eqPattern
                | "[" listPatternInner)
pattern = listPattern:p ("?" order:e -> t.SuchThatPattern(p, e)
                            | -> p)

basic = docoDef | escapeExpr | forExpr | ifExpr | lambdaExpr | metaExpr | switchExpr | tryExpr | whileExpr | whenExpr

docoDef = br doco?:doc (functionExpr:o -> t.Object(doc, *o)
                       |objectExpr:o -> t.Object(doc, *o)
                       |interfaceExpr:i -> t.Interface(doc, *i))

indentDocoDef = br doco?:doc (indentFunctionExpr:o -> t.Object(doc, *o)
                             |indentObjectExpr:o -> t.Object(doc, *o)
                             |indentInterfaceExpr:i -> t.Interface(doc, *i))

doco = tok('DOC_COMMENT'):doc br -> doc.strip()
objectExpr = (('object' objectName:n) | keywordPattern:n) objectTail:tail -> [n, tail]
indentObjectExpr = (('object' objectName:n) | keywordPattern:n)
                   indentObjectTail:tail -> [n, tail]

functionExpr = (('def' objectName:n) | keywordPattern:n) functionTail:tail -> [n, tail]
indentFunctionExpr = (('def' objectName:n) | keywordPattern:n) indentFunctionTail:tail -> [n, tail]

objectName = ('_' optGuard:e -> t.IgnorePattern(e)
               |namePattern)
objectTail = (('extends' br order)?:e oAs?:g oImplements:oi scriptPair:s
                  -> t.Script(e, g, oi, *s))
indentObjectTail = (('extends' br order)?:e oAs?:g oImplements:oi indentScriptPair:s
                     -> t.Script(e, g, oi, *s))

oAs = 'as' br order
oImplements = ('implements' br order:x ("," order)*:xs -> [x] + xs
               | -> [])
functionTail = parenParamList:ps optResultGuard:g oImplements:fi block:b -> t.Function(ps, g, fi, b)
indentFunctionTail = parenParamList:ps optResultGuard:g oImplements:fi indentBlock:b -> t.Function(ps, g, fi, b)

parenParamList = "(" (pattern:p ("," pattern)*:ps ')' -> [p] +  ps
                       | ')' -> [])
optResultGuard = (':' guard)?
scriptPair = "{" method*:methods matcher*:matchers br '}' -> [methods, matchers]
indentScriptPair = ':' EOLs tok('INDENT') br pass indentMethod*:methods indentMatcher*:matchers br tok('DEDENT') -> [methods, matchers]

method = br (doco?:doc (("to" -> t.To) | "method" -> t.Method):to verb?:v parenParamList:ps optResultGuard:g block:b  -> to(doc, v, ps, g, b))
indentMethod = br (doco?:doc (("to" -> t.To) | "method" -> t.Method):to verb?:v parenParamList:ps optResultGuard:g indentBlock:b  -> to(doc, v, ps, g, b))
matcher = 'match' pattern:p block:b -> t.Matcher(p, b)
indentMatcher = 'match' pattern:p indentBlock:b -> t.Matcher(p, b)

interfaceExpr = ('interface' objectName:n iguards?:g ((multiExtends:es oImplements:oi iscript:s -> [n, g, es, oi, s])
                       |parenParamDescList:ps optGuard:rg -> [n, g, [], [], t.InterfaceFunction(ps, rg)]))
indentInterfaceExpr = ('interface' objectName:n iguards?:g ((multiExtends:es oImplements:oi indentIScript:s -> [n, g, es, oi, s])
                       |parenParamDescList:ps optGuard:rg -> [n, g, [], [], t.InterfaceFunction(ps, rg)]))
iguards = 'guards' pattern
multiExtends = (('extends' br order:x ("," order)*:xs -> [x] + xs)
                 | -> [])
iscript = "{" (messageDesc:m br -> m)*:ms '}' -> ms
indentIScript = ':' EOLs tok('INDENT') br pass (messageDesc:m br -> m)*:ms br tok('DEDENT') -> ms
messageDesc = br (doco?:doc ("to" | "method"):to verb?:v parenParamDescList:ps optGuard:g
                -> t.MessageDesc(doc, to, v, ps, g))
paramDesc = (justNoun | ('_') -> None):n optGuard:g -> t.ParamDesc(n, g)
parenParamDescList = "(" paramDesc:p ("," paramDesc)*:ps ')' -> [p] +  ps

escapeExpr = 'escape' pattern:p block:b catcher?:c -> t.Escape(p, b, c)
indentEscapeExpr = 'escape' pattern:p indentBlock:b indentCatcher?:c -> t.Escape(p, b, c)

forExpr = 'for' forPattern:p 'in' br assign:a block:b catcher?:c -> t.For(*(p + [a, b, c]))
indentForExpr = 'for' forPattern:p 'in' br assign:a indentBlock:b indentCatcher?:c -> t.For(*(p + [a, b, c]))

forPattern = pattern:p (br "=>" pattern:px -> [p, px]
                           | -> [None, p])

ifExpr = 'if' parenExpr:p br block:b ('else' (ifExpr | block) | -> None):e -> t.If(p, b, e)

indentIfExpr = 'if' parenExpr:p br indentBlock:b (br 'else' (indentIfExpr | indentBlock) | -> None):e -> t.If(p, b, e)

lambdaExpr = br doco?:doc 'fn' patterns:ps block:b -> t.Lambda(doc, ps, b)

metaExpr = 'meta' "." identifier:i (?(i == 'getState') -> "State"
                                   |?(i == 'scope') -> "Scope"
                                   |?(i == 'context') -> "Context"):s "(" ')' -> t.Meta(s)
switchExpr = 'switch' parenExpr:e "{" (matcher:m br -> m)*:ms '}' -> t.Switch(e, ms)
indentSwitchExpr = 'switch' parenExpr:e ':' EOLs tok('INDENT') br (indentMatcher:m br -> m)*:ms tok('DEDENT') -> t.Switch(e, ms)

tryExpr = 'try' block:tb catcher*:cs ('finally' block)?:fb -> t.Try(tb, cs, fb)
indentTryExpr = 'try' indentBlock:tb indentCatcher*:cs (br 'finally' indentBlock)?:fb -> t.Try(tb, cs, fb)
catcher = 'catch' pattern:p block:b -> t.Catch(p, b)
indentCatcher = br 'catch' pattern:p indentBlock:b -> t.Catch(p, b)

whileExpr = 'while' parenExpr:e block:b catcher?:c -> t.While(e, b, c)
indentWhileExpr = 'while' parenExpr:e indentBlock:b indentCatcher?:c -> t.While(e, b, c)

whenExpr = 'when' parenArgs:a br "->" block:b catcher*:cs ('finally' block)?:fb -> t.When(a, b, cs, fb)
indentWhenBlock = '->' EOLs tok('INDENT') br pass (indentSeq |-> t.SeqExpr([])):s br
               tok('DEDENT') -> s
indentWhenExpr = 'when' parenArgs:a indentWhenBlock:b indentCatcher*:cs (br 'finally' indentBlock)?:fb -> t.When(a, b, cs, fb)

topSeq = topExpr:x (seqSep topExpr)*:xs seqSep? -> t.SeqExpr([x] + xs)
pragma = 'pragma' "." verb:v "(" string:s ')' -> t.Pragma(v, s)
topExpr = (pragma -> t.NounExpr("null")) | blockExpr
start = br topSeq?
